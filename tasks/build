;; -*- mode: fennel; -*- vi:ft=fennel

(fn rev-count []
  (with-open [f (io.popen "git -C . rev-list HEAD --count")]
    (f:read)))

(local (MAJOR MINOR PATCH)
  (values "0" "0" (rev-count)))

(local fennel
  (require :fennel))

(with-open [http (io.open :http/init.fnl :r)
            out  (io.open :http.lua :w)]
  (let [src (-> (http:read :*a)
                (string.gsub
                 ":__VERSION :dev"
                 (string.format ":__VERSION \"%s.%s.%s\"" MAJOR MINOR PATCH)))]
    (case (xpcall #(fennel.compile-string
                    src
                    {:requireAsInclude true
                     :metadata true
                     :skipInclude [:socket :ltn12 :reduced]})
                  fennel.traceback)
      (true lua-src) (out:write lua-src)
      (_ message) (do (io.stderr:write message "\n")
                      (os.exit 1)))))
